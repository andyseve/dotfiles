# Author: Anish Sevekari
# Last Edited: Tue 24 Mar 2020 10:44:09 PM EDT

# Set zsh variables and source corresponding settings
ZSH_HOME=$HOME/.zsh
fpath=($ZSH_HOME/functions $ZSH_HOME/completions $fpath)

################################################################################
# Zplugin ######################################################################
################################################################################

source $HOME/.zplugin/bin/zplugin.zsh
autoload -Uz _zplugin
(( ${+_comps} )) && _comps[zplugin]=_zplugin

zplugin light hlissner/zsh-autopair
zplugin light jeffwalter/zsh-plugin-cd-ssh
zplugin light arzzen/calc.plugin.zsh
zplugin light zsh-users/zsh-autosuggestions
zplugin light srijanshetty/zsh-pip-completion
zplugin ice atclone"./install.py"; zplugin light wting/autojump
zplugin ice pick"async.zsh" src"pure.zsh"; zplugin light sindresorhus/pure 
zplugin ice from"gh-r" as"command"; zplugin load junegunn/fzf-bin
zplugin light zdharma/fast-syntax-highlighting
zplugin light zsh-users/zsh-history-substring-search

################################################################################
# Settings #####################################################################
################################################################################

KEYTIMEOUT=1 # keytiming 10ms
CASE_SENSITIVE="false" # case sensative searches
HYPHEN_INSENSITIVE="true" # hyphen insensative searches
unsetopt beep # removes beeps in windows

setopt histreduceblanks # remove blanks
setopt histignorealldups # ignore repeats in history
setopt sharehistory # share history between terminals

setopt correct # spelling correction
COMPLETION_WAITING_DOTS="true"
bindkey -v # Use vim keybindings


# Keep 1000 lines of history within the shell and save it to ~/.zsh_history:
HISTSIZE=1000
SAVEHIST=1000
HISTFILE=~/.zsh_history


# zsh/autosuggestions color change
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=10'



################################################################################
# Keybindings ##################################################################
################################################################################

# Search
bindkey -M vicmd '?' history-incremental-search-backward
bindkey -M vicmd '/' history-incremental-search-forward
# history search keybindings
bindkey "$terminfo[kcuu1]" history-substring-search-up
bindkey "$terminfo[kcud1]" history-substring-search-down
bindkey -M vicmd 'k' history-substring-search-up
bindkey -M vicmd 'j' history-substring-search-down

################################################################################
# Functions and Commands #######################################################
################################################################################

# Importing functions and commands
for file in $ZSH_HOME/functions/*.zsh; do
	source $file
done

_has() {
  return $( whence $1 >/dev/null )
}
_color() {
  return $( [ -z "$INSIDE_EMACS" ] )
}

# run neofetch at start of ssh session
if [[ -n $SSH_CONNECTION ]]; then
	if _has neofetch; then
		neofetch
	fi
fi

# loading autojump
[[ -s $HOME/.autojump/etc/profile.d/autojump.sh ]] && source $HOME/.autojump/etc/profile.d/autojump.sh



# FZF config
if _has fzf; then
	if _has ag; then
		export FZF_DEFAULT_COMMAND='ag -g ""' # Use Ag if it exists
		export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
		export FZF_ALT_C_COMMAND="$FZF_DEFAULT_COMMAND"
	fi
  export FZF_DEFAULT_OPTS='
		--height 40% --reverse
  '
fi

################################################################################
# Aliases ######################################################################
################################################################################

# use colored versions of commands by default
if [ -x /usr/bin/dircolors ]; then
	if test -r ~/.zsh/dircolors; then
		eval "$(dircolors -b ~/.zsh/dircolors)"
	else
		eval "$(dircolors -b)"
	fi
	alias ls='ls --color=auto'
	alias dir='dir --color=auto'
	alias vdir='vdir --color=auto'

	alias grep='grep --color=auto'
	alias fgrep='fgrep --color=auto'
	alias egrep='egrep --color=auto'
fi

# some more ls aliases
alias ll='ls -alF'
alias la='ls -A'
alias l='ls -CF'

# Add an "alert" alias for long running commands.  Use like so:
#   sleep 10; alert
alias alert='notify-send --urgency=low -i "$([ $? = 0 ] && echo terminal || echo error)" "$(echo $history[$HISTCMD]|sed -e '\''s/^\s*[0-9]\+\s*//;s/[;&|]\s*alert$//'\'')"'

# Importing Aliases
for file in $ZSH_HOME/aliases/*.zsh; do
	source $file
done

################################################################################
# Definitions ##################################################################
################################################################################

export PAGER='less'
if _has nvim; then
	export EDITOR='nvim'
else
	export EDITOR='vim'
fi

PATH="$HOME/bin:$PATH" # add my scripts to bin
PATH="$HOME/.local/bin:$PATH" # numpy scripts
if [ -e "$HOME/bin/miniconda3/bin" ]; then
	PATH="$HOME/bin/miniconda3/bin:$PATH" # add anaconda to path
fi
if [ -e "$HOME/.cabal/bin" ]; then
	PATH="$HOME/.cabal/bin:$PATH" # add cabal to path
fi


################################################################################
# Prompt #######################################################################
################################################################################
autoload -U promptinit && promptinit

PURE_PROMPT_SYMBOL='‚ùØ'
PROMPT='%(?.%F{green}.%F{red})${PURE_PROMPT_SYMBOL}%f '
PROMPT='%F{white}%* '$PROMPT

precmd_pipestatus() {
	RPROMPT="%F{red}${(j.|.)pipestatus}"
       if [[ ${(j.|.)pipestatus} = 0 ]]; then
              RPROMPT=""
       fi
}
add-zsh-hook precmd precmd_pipestatus

################################################################################
# Completions ##################################################################
################################################################################

# Use modern completion system
autoload -Uz compinit && compinit -u

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _correct _approximate
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' menu select=2
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=*'
zstyle ':completion:*' menu select=long
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true

zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'

# vim:ft=zsh
